ext {
    // Name for test output jar.
    testNameJar = "$project.name-test"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task testJar(type: Jar) {
    from sourceSets.test.output
}

task testSourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.test.allJava
}

publishing {
    publications {
        commonJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

        }
        commonTestJava(MavenPublication) {
            artifactId testNameJar
            artifact testJar
            artifact testSourceJar
        }
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifactory {
    contextUrl = "http://artifacts.openmicroscopy.org/artifactory"
    publish {
        repository {
            repoKey = 'integration-libs' // The Artifactory repository key to publish to
            username = System.getenv('ARTIFACTORY_USER') ?: "${findProperty('artifactorUser')}"
            password = System.getenv('ARTIFACTORY_PASSWORD') ?: "${findProperty('artifactorPassword')}"
        }
    }
}